group 'pro.tompark.swingheil'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
        querydslVersion = '4.1.4'
        metaModelsSourcesDir = file("src/main/genq")
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
        maven { url "http://repo.spring.io/milestone" }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

configurations {
    querydslapt
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8


repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
    maven { url "http://repo.spring.io/milestone" }
}

sourceSets {
    main {
        java {
            srcDir metaModelsSourcesDir
        }
    }
}

dependencies {
    compile("javax.inject:javax.inject:1")
    compile("com.h2database:h2")
    compile("org.springframework.boot:spring-boot-starter")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")

    //QueryDSL
    compile("com.querydsl:querydsl-core:${querydslVersion}")
    compile("com.querydsl:querydsl-jpa:${querydslVersion}")
    querydslapt("com.querydsl:querydsl-apt:${querydslVersion}")

    testCompile("org.springframework.boot:spring-boot-starter-test")
}

task querymodels(type: JavaCompile, group: 'build') {
    doFirst {
        delete metaModelsSourcesDir;
        metaModelsSourcesDir.mkdirs();
    }

    classpath = configurations.compile + configurations.querydslapt
    destinationDir = metaModelsSourcesDir

    source = sourceSets.main.java
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor",
            "-s", metaModelsSourcesDir
    ]
}
